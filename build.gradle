buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.1.0'
        classpath group: 'de.dynamicfiles.projects.gradle.plugins', name: 'javafx-gradle-plugin', version: '8.8.2'
    }
}

plugins {
    id 'java'
    id 'jacoco'
}

group 'com.domination'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'javafx-gradle-plugin'

repositories {
    mavenCentral()
}

dependencies {
    testCompile(
            'org.junit.jupiter:junit-jupiter-api:5.1.0',
            'org.jmockit:jmockit:1.41',
    )
    testRuntime(
            'org.junit.jupiter:junit-jupiter-engine:5.1.0'
    )
    compile(
            'com.opencsv:opencsv:4.1'
    )
}

test {
    jvmArgs "-javaagent:${classpath.find { it.name.contains("jmockit") }.absolutePath}"
}

project.afterEvaluate {
    def junitPlatformTestTask = project.tasks.getByName('junitPlatformTest')

    jacoco {
        toolVersion = "0.8.2"
        applyTo junitPlatformTestTask
    }

    // create junit platform jacoco task
    project.task(type: JacocoReport, "junitPlatformJacocoReport",
            {
                sourceDirectories = files("./src/")
                classDirectories = files("$buildDir/classes/java/main")
                reports {
                    xml.enabled = true
                    html.enabled = true
                    xml.destination file("$buildDir/reports/jacoco/test/jacocoTestReport.xml")
                    html.destination file("$buildDir/reports/jacoco/test/jacocoTestReport.html")
                }
                executionData junitPlatformTestTask
                onlyIf = {
                    true
                }
                doFirst {
                    try {
                        classDirectories = files(classDirectories.files.collect {
                            fileTree(dir: it, excludes: ['views/**', '**/Screen.class'])
                        })
                    } catch (e) {
                        println "jacoco: no exclusion patterns due to ${e.message}"
                    }
                }
            })
}

jfx {
    // minimal requirement for jfxJar-task
    mainClass = 'Main'

    // minimal requirement for jfxNative-task
    vendor = 'DomiNation'
}